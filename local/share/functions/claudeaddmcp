claudeaddmcp() {
  emulate -L zsh

  local server_name="$1"

  typeset -A mcp_configs
  mcp_configs=(
    atlassian "https://mcp.atlassian.com/v1/sse"
    playwright "mcp-server-playwright"
    context7 "https://mcp.context7.com/mcp"
  )
  typeset -A mcp_transports
  mcp_transports=(
    atlassian "sse"
    playwright "stdio"
    context7 "http"
  )
  typeset -A mcp_scopes
  mcp_scopes=(
    atlassian "user"
    playwright "user"
    context7 "user"
  )
  typeset -A mcp_descriptions
  mcp_descriptions=(
    atlassian "Jira MCP server"
    playwright "Playwright browser automation server"
    context7 "Context7 server (requires API key)"
  )
  typeset -A mcp_auth_headers
  mcp_auth_headers=(
    context7 "CONTEXT7_API_KEY"
  )

  if [[ -z "$server_name" ]]; then
    echo "Usage: claudeaddmcp <server_name>"
    echo "Available servers:"
    for key in ${(k)mcp_descriptions}; do
      printf "  %-12s - %s (scope: %s)\n" "$key" "${mcp_descriptions[$key]}" "${mcp_scopes[$key]:-local}"
    done
    return 1
  fi

  if [[ -z "${mcp_transports[$server_name]}" ]]; then
    echo "Error: Unknown MCP server '$server_name'"
    echo "Run 'claudeaddmcp' without arguments to see available servers"
    return 1
  fi

  local transport="${mcp_transports[$server_name]}"
  local config="${mcp_configs[$server_name]}"
  local scope="${mcp_scopes[$server_name]:-local}"
  local auth_header="${mcp_auth_headers[$server_name]}"

  local api_key=""
  if [[ -n "$auth_header" ]]; then
    # Check for environment variable MCP_{header_name}
    local env_var_name="MCP_${auth_header}"
    api_key="${(P)env_var_name}"

    if [[ -z "$api_key" ]]; then
      echo "Error: Environment variable $env_var_name is not set"
      echo "Please set it with: export $env_var_name='your-api-key'"
      return 1
    fi

    echo "Using API key from environment variable $env_var_name"
  fi

  echo "Adding MCP server: $server_name"
  echo "Scope: $scope"
  echo "Transport: $transport"
  echo "Config: $config"

  if [[ "$transport" == "stdio" ]]; then
    claude mcp add --scope "$scope" "$server_name" -- "$config"
  elif [[ -n "$auth_header" ]]; then
    claude mcp add --scope "$scope" --transport "$transport"  "$server_name" "$config" --header "${auth_header}:${api_key}"
  else
    claude mcp add --scope "$scope" --transport "$transport" "$server_name" "$config"
  fi

  if [[ $? -eq 0 ]]; then
    echo "Successfully added $server_name MCP server"
  else
    echo "Failed to add $server_name MCP server"
    return 1
  fi
}

